/* Construct Common Shocks Associated with Series 
*/

new;
outwidth 256;
library pgraph;
graphset;
xlsname="c:\\usr\\crisis\\ddisk\\data\\bpea_fac_1.xlsx";                    @ DATA Set (Excel File) @
outdir="c:\\usr\\crisis\\ddisk\\gss\\supplement\\out\\";
figdir="c:\\usr\\crisis\\ddisk\\gss\\supplement\\fig\\";

@ -- Date Parameters for OLS Estimated of lambda and F -- @
nest_fy = 1959;
nest_fq = 3;
nest_ly = 2007;
nest_lq = 3;

@ -- Parameters for full sample estimates of F (conditional on Lambda) -- @
nest_fy_fs = 1959;
nest_fq_fs = 3;
nest_ly_fs = 2011;
nest_lq_fs = 2;

@ -- Factor VAR Sample Period -- @
nest_fy_var = 1959;
nest_fq_var = 3;
nest_ly_var = 2007;
nest_lq_var = 3;


@ Demeaning @
idemean = 1;   @ 0 Do Nothing
               1 Eliminate low-frequency by local Demeaning
               2 Eliminate low-frequency trend by full-sample demeaning @
@ Bi-Weight Parameter for local demeaning @
bw_bw = 100;
   
@ Factor Parameters @
nfac = 5;
nols_it = 200;   @ Number of OLS iterations (for unbalanced panel @

@ VAR Parameters @
var_method = 1;  @ 1 = full sample;  @
var_lag = 4;     @ Number of lags in factor VAR @
icomp=1;
ivarmat=0;
i_const = 0;     @ 0 = do not include constant in VAR @
nimp = 100;      @ Number of IRF to Compute @

if var_method ./= 1;
 "Factor_VAR.gss uses time-invariant VAR -- stopping ";
 stop;
endif;

ofile = outdir $+ "Big_Shocks_7factors.asc";
output file = ^ofile reset; output on;


@ -- Category Names --- @
cat_name = 
"NIPA" $|
"IP"   $|
"Emp/Unemp" $|
"Hous.Starts" $|
"Orders/Inv/Sales" $|
"Prices" $|
"EmpEarning/Productivity" $|
"Int. Rates" $|
"Money&Credit" $|
"StockPrices&Wealth" $|
"Housing Prices" $|
"Exch.Rates" $|
"" $|
"Other";

#include canon.prc;
#include var.prc;
#include fcst.prc;
#include readxls.prc;
#include bw_trend.prc;
#include y4plot.prc;
#include calendar.gss;
#include datain_1.gss;

@ -- Peak Dates for Comparisons -- @
pdates = 
(1960~2) |
(1969~4) |
(1973~4) |
(1980~1) |
(1981~3) |
(1990~3) |
(2001~1) |
(2007~4) ;

@ -- Trough Dates for Comparisons -- @
tdates = 
(1961~1) |
(1970~4) |
(1975~1) |
(1980~3) |
(1982~4) |
(1991~1) |
(2001~4) |
(2009~2) ;

@ String Vector with Dates @
str_tdates = "";
i = 1; do while i <= rows(tdates);
	tmp = "" $+ ftocv(tdates[i,1],4,0) $+ ":" $+ ftocv(tdates[i,2],1,0);
	if i .== 1;
	 str_tdates = tmp;
	else;
	 str_tdates = str_tdates $| tmp;
	endif;
i = i+1; endo;

i_tdates = missmat(rows(tdates),1);  @ Indicators for trough dates @
i = 1; do while i <= rows(tdates);
	cdate = tdates[i,1]+(tdates[i,2]-1)/4;
	i_tdates[i] = minindc(abs(calvec-cdate));
i = i+1; endo;


"Number of Series in dataset = ";;cols(bpdata);
nx=cols(bpdata);

@ Estimate Reduced Form Factor and Lambda @
#include rr_factor.gss;

@ Data, Fitted Values, and Residuals @
  ydata = bpdata;
  yfit_rf = fac_rf*lambda_rf';                                     
  yres_rf = ydata-yfit_rf;

@ Estimate VAR for Factors @
  /* m = companion matrix without constant
     m_const = companion matrix with constant @ Last Element is constant @
     seps = covariance matrix of residuals
     eps = residuals
     eps_lr = long-run residuals .. [I - Phi1 - ... - Phin]^-1 * eps;
  */
#include factor_var.gss;

y = fac_rf;
i=1; do while i<=var_lag;
 x1 = missmat(dnobs,nfac); 
 x1[1+i:dnobs,.]=fac_rf[1:dnobs-i,.];
 if i .== 1;
  x = x1;
 else;
  x = x~x1;
 endif;
i = i+1; endo;
eps_fs = y - x*beta;
yeps_rf = eps_fs*lambda_rf';

sname =
"GDPC96" $|
"PCECC96" $|
"GPDIC96" $|
"PAYEMS" $|
"OPHNFB" $|
"HOUST" $|
"WPU0561" $|
"FEDFUNDS" $|
"MED3_TB3M" $|
"MVOL" $|
"TNWBSHNO";

eps_series = missmat(dnobs,rows(sname));
i = 1; do while i <= rows(sname);
	ii=maxindc(bpnamevec .$== sname[i]);
	eps_series[.,i] = yeps_rf[.,ii];
i = i+1; endo;

@ Compute Quantiles for Each Shock @
i1 = calvec .< 2007.74;
i2 = (calvec .< 2007.74).*(calvec .> 1983.99);
i3 = (calvec .> 2006.99);
eps1 = selif(eps_series,i1);
eps2 = selif(eps_series,i2);
eps3 = selif(eps_series,i3);
eps1p = packr(eps1);
std = sqrt(meanc(eps1p.^2));
eps1_std = eps1./std';
eps2_std = eps2./std';
eps3_std = eps3./std';
eps1p_std = packr(eps1_std);
eps2p_std = packr(eps2_std);
cv = selif(calvec,i3);
rslt1 = eps3_std;
rslt2 = missmat(rows(eps3_std),cols(eps3_std));
rslt3 = missmat(rows(eps3_std),cols(eps3_std)); 

i = 1; do while i <= rows(eps3_std);
	j = 1; do while j <= cols(eps3_std);
		rslt2[i,j] = meanc(eps1p_std[.,j] .<= eps3_std[i,j]);
		rslt3[i,j] = meanc(eps2p_std[.,j] .<= eps3_std[i,j]);	
	j = j+1; endo;
i = i+1; endo;

"Standardized Shocks";
"";
"Date,";;format /rd 2,0;
i = 1; do while i <= rows(sname);
	sname[i];;
	if i .< rows(sname);
	 ",";;
	else;
	 "";
	endif;
i = i + 1; endo;
tmp = cv~rslt1;
i=1; do while i <= rows(tmp);
	format /rd 6,2;
	tmp[i,1];;",";;
	format /rd 4,1;
	prtmat_comma(tmp[i,2:cols(tmp)]);
i = i+1; endo;	
"";"";


output off;

